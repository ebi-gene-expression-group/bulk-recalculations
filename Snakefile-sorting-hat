import yaml
import glob

# atom: set grammar=python:

metadata = {}

def get_lsf_config_file():
    """
    Returns the rule-specific LSF resource settings file to be copied to
    acc working directory before the snakemake call
    """
    if 'lsf_config' in config:
        return config['lsf_config']
    else:
        return None


def fill_metadata(acc):
    with open(f"{acc}/{acc}.metadata_summary.yaml", 'r') as meta:
        metadata[acc] = yaml.load(meta, Loader=yaml.FullLoader)

def get_log_handler_script():
    if 'loghandler' in config:
        return f"--log-handler-script {config['loghandler']} &>/dev/null"
    return ""

def get_tool(acc):
    """
    If a tool is given in the config, then that tool is preferred,
    otherwise it is determined by the experiment type.
    """
    if 'tool' in config:
        return config['tool']
    if acc not in metadata:
        fill_metadata(acc)
    if 'experiment_type' not in metadata[acc]:
        print(f"Experiment type not found for {acc}")
        return None
    if 'differential' in metadata[acc]['experiment_type']:
        return "all-diff"
    if 'baseline' in metadata[acc]['experiment_type']:
        return "all-baseline"
    print(f"Could not determine tool for {acc}")
    return None


def get_organism(acc):
    if acc not in metadata:
        fill_metadata(acc)
    return metadata[acc]['organism']

def get_exp_type(acc):
    if acc not in metadata:
        fill_metadata(acc)
    return metadata[acc]['experiment_type']

def get_assay_contrast_line(acc):
    if acc not in metadata:
        fill_metadata(acc)
    if 'contrasts' in metadata[acc]:
        contrast_ids = "::".join(metadata[acc]['contrasts'].keys())
        contrast_labels = "&&".join(metadata[acc]['contrasts'].values())
        return f"contrast_ids={contrast_ids} contrast_labels=\"{contrast_labels}\""
    elif 'assays' in metadata[acc]:
        assay_ids = "::".join(metadata[acc]['assays'].keys())
        assay_labels = "&&".join(metadata[acc]['assays'].values())
        return f"assay_ids={assay_ids} assay_labels=\"{assay_labels}\""
    return None

def get_gff_path(acc):
    if acc not in metadata:
        fill_metadata(acc)
    return metadata[acc]['gff']

def get_bioentities_property_path():
    return config['bioentities_properties']

def get_skip_steps_file():
    if 'skip_steps_file' in config:
        return config['skip_steps_file']
    else:
        return None

def get_check_sp_file():
  if 'check_sp_file' in config:
    return config['check_sp_file']
  else:
    return None

def get_methods_template_baseline():
    if 'methods_base' in config:
        return config['methods_base']
    else:
        return None

def get_methods_template_differential():
    if 'methods_dif' in config:
        return config['methods_dif']
    else:
        return None

def get_isl_dir():
    if 'isl_dir' in config:
        return config['isl_dir']
    else:
        return None

def get_tmp_dir():
    if 'tmp_dir' in config:
        return config['tmp_dir']
    else:
        return None

def get_goal():
    if 'goal' in config:
        return config['goal']
    else:
        return None


def get_outputs():
    import os
    ACC=[]
    if 'accessions' in config:
        ACC=config['accessions'].split(":")
    else:
        ACC=glob.glob('E-*')

    if 'goal' in config:
        if config['goal'] == 'recalculations':

            if 'atlas_exps' in config:
                # filter out ACC based on accessions that are available in atlas_exps
                valid_acc_dir = next(os.walk( config['atlas_exps'] ))[1]
                # intersection of the two lists
                pass_ACC = list(set(ACC) & set(valid_acc_dir))
                return expand("{accession}/{accession}.recalculations.done", accession=pass_ACC)
            else:
                return expand("{accession}/{accession}.recalculations.done", accession=ACC)
        elif config['goal'] == 'reprocess':
            return expand("{accession}/{accession}.reprocess.done", accession=ACC)
        else:
            return None
    else:
        return None


localrules: all

wildcard_constraints:
    accession = "E-\D+-\d+"

rule all:
    input:
        required_outputs=get_outputs()


rule get_experiment_metadata:
    container:
        "docker://quay.io/biocontainers/perl-atlas-modules:0.2.0--h1b792b2_0"
    conda:
        "envs/perl-atlas-modules.yaml"
    log: "{accession}/logs/get_experiment_metadata.log"
    params:
        atlas_meta_config = config['atlas_meta_config'],
        atlas_prod = config['atlas_prod'],
        atlas_exps = config['atlas_exps'],
        check_species_file=get_check_sp_file()
    input:
        config_file = "{accession}/{accession}-configuration.xml",
        gtf_dir = config['gtf_dir']
    output:
        metadata = "{accession}/{accession}.metadata_summary.yaml"
    shell:
        """
        # needed for container conda env activation
        # /usr/local/env-execute
        set -e # snakemake on the cluster doesn't stop on error when --keep-going is set
        mkdir -p {wildcards.accession}/logs
        exec &> {log}
        export ATLAS_META_CONFIG={params.atlas_meta_config}
        export ATLAS_PROD={params.atlas_prod}
        export ATLAS_EXPS={params.atlas_exps}
        source {workflow.basedir}/bin/metadata_retrieval_functions.sh

        perl {workflow.basedir}/bin/get_exptype_contrasts_assays.pl {wildcards.accession} {input.config_file} {output.metadata}
        if [ -e "{params.atlas_exps}/{wildcards.accession}/{wildcards.accession}.condensed-sdrf.tsv" ]; then
            echo "Trying with condensed in atlas_exps..."
            organism=$(get_organism_from_condensed_sdrf "{params.atlas_exps}/{wildcards.accession}/{wildcards.accession}.condensed-sdrf.tsv")
        elif [ -e "{wildcards.accession}/{wildcards.accession}.condensed-sdrf.tsv" ]; then
            echo "Trying with condensed in local experiment dir..."
            organism=$(get_organism_from_condensed_sdrf "{wildcards.accession}/{wildcards.accession}.condensed-sdrf.tsv")
        elif [ -e "{wildcards.accession}/{wildcards.accession}-configuration.xml" ]; then
            echo "Trying with perl (produces organism with capitals and spaces)"
            organism=$(perl {workflow.basedir}/bin/get_experiment_info.pl --experiment {wildcards.accession} --xmlfile {wildcards.accession}/{wildcards.accession}-configuration.xml --organism | sed 's/ /_/g' | tr '[:upper:]' '[:lower:]')
        else
            echo "Nothing worked...Exiting with error"
            >&2 echo "Can't retrieve organism: neither {wildcards.accession}.condensed-sdrf.tsv nor {wildcards.accession}/{wildcards.accession}-configuration.xml not found "
            exit 1
        fi
        # fix species name
        while read line; do
            echo "$line"
            DIR=$line
        done < <(perl {workflow.basedir}/bin/bulk_recalculations_check_species_names.pl $(echo $organism) {params.check_species_file}  )
	organism_check=$(echo $DIR)

        ##echo $organism "check-> "$organism_check >> organisms.txt
        organism=$organism_check

        gff_file=$( find {input.gtf_dir}/$organism -iname "$organism.*.gff3" | head -n1)
        if [ -z "$gff_file" ]; then
            gff_file=$( find {input.gtf_dir}/$organism -iname "$organism_*.gff3" | head -n1)
        fi
	echo "GTF file location sourced"
        magetabfiles=$( perl {workflow.basedir}/bin/get_experiment_info.pl --experiment {wildcards.accession} --xmlfile {wildcards.accession}/{wildcards.accession}-configuration.xml --magetabfiles --not-strict)
	echo "MAGETAB location sourced"
        idf=$( echo $magetabfiles | awk -F',' '{{ print $1 }}' )
        sdrf=$( echo $magetabfiles | awk -F',' '{{ print $2 }}' )
        if [ -z "$gff_file" ] || [ -z "$idf" ] || [ -z "$sdrf" ]; then
            echo "GFF: $gff_file"
            echo "IDF: $idf"
            echo "SDRF: $sdrf"
            echo "GFF, IDF or SDRF not found. YAML not created...Exiting with error"
            exit 1
        else
            echo "organism: $organism" >> {output.metadata}
            echo "gff: $gff_file" >> {output.metadata}
            echo "idf: $idf" >> {output.metadata}
            echo "sdrf: $sdrf" >> {output.metadata}
        fi
        """

rule produce_reprocess_call:
    input:
        metadata = "{accession}/{accession}.metadata_summary.yaml",
        working_directory = "{accession}"
    log: "{accession}/logs/produce_reprocess_call.log"
    params:
        bioentities_properties=get_bioentities_property_path(),
        skip_steps_file=get_skip_steps_file(),
        methods_base=get_methods_template_baseline(),
        methods_dif=get_methods_template_differential(),
        isl_dir=get_isl_dir(),
        tmp_dir=get_tmp_dir(),
        lsf_config = get_lsf_config_file(),
        log_handler_script=get_log_handler_script(),
        goal=get_goal()
    output:
        touch("{accession}/{accession}.reprocess.done")
    run:
        command_line=f"""
        set -e # snakemake on the cluster doesn't stop on error when --keep-going is set
        mkdir -p {wildcards.accession}/logs
        exec &> {log}
        pushd {input.working_directory}
        cp {params.lsf_config} lsf.yaml

        snakemake --use-singularity --restart-times 1 --latency-wait 100 {config['sm_options']} --config \
            accession={wildcards.accession} \
            goal={params.goal} \
            tool={get_tool(wildcards.accession)} \
            metadata_summary=../{input.metadata} \
            bioentities_properties={params.bioentities_properties} \
            skip_steps_file={params.skip_steps_file} \
            methods_base={params.methods_base} \
            methods_dif={params.methods_dif} \
            isl_dir={params.isl_dir} \
            tmp_dir={params.tmp_dir} \
            -s {workflow.basedir}/Snakefile-reprocess {params.log_handler_script}
        popd
        """
        #print(command_line)
        shell(command_line)


rule produce_recalculations_call:
    input:
        metadata = "{accession}/{accession}.metadata_summary.yaml",
        working_directory = "{accession}"
    log: "{accession}/logs/produce_recalculations_call.log"
    params:
        bioentities_properties=get_bioentities_property_path(),
        skip_steps_file=get_skip_steps_file(),
        lsf_config = get_lsf_config_file(),
        log_handler_script=get_log_handler_script(),
        goal=get_goal()
    output:
        touch("{accession}/{accession}.recalculations.done")
    run:
        command_line=f"""
        set -e # snakemake on the cluster doesn't stop on error when --keep-going is set
        mkdir -p {wildcards.accession}/logs
        exec &> {log}
        pushd {input.working_directory}
        if ! [ -z {params.lsf_config} ]; then
            if [ ! -f "lsf.yaml" ]; then
                ln -s {params.lsf_config} lsf.yaml
            fi
        fi

        snakemake --restart-times 4 --latency-wait 250 {config['sm_options']} --config \
            accession={wildcards.accession} \
            goal={params.goal} \
            tool={get_tool(wildcards.accession)} \
            metadata_summary=../{input.metadata} \
            bioentities_properties={params.bioentities_properties} \
            skip_steps_file={params.skip_steps_file} \
            -s {workflow.basedir}/Snakefile-recalculations {params.log_handler_script}
        popd
        """
        #print(command_line)
        shell(command_line)
